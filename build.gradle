plugins {
    id("java")
    id("pmd")
    id("jacoco")
    id("checkstyle")
    id("org.sonarqube")
    id("com.google.cloud.tools.jib") apply false
    id("org.springframework.boot") apply false
    id("io.spring.dependency-management") apply false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs
            }
        }
    }
}

sonarqube {
    def exclusions = extractQClass()
    file("${rootDir}/config/jacoco/exclude-coverage.txt").eachLine {
        exclusions.add(it)
    }

    properties {
        property("sonar.projectKey", System.getenv("SONAR_PROJECT_KEY"))
        property("sonar.organization", "dailyge")
        property("sonar.host.url", System.getenv("SONAR_HOST_URL"))
        property("sonar.login", System.getenv("SONAR_TOKEN"))
        property("sonar.language", "Java")
        property("sonar.sourceEncoding", "UTF-8")
        property("sonar.exclusions", exclusions.join(", "))
        property("sonar.java.coveragePlugin", "jacoco")
        property(
                "sonar.coverage.jacoco.xmlReportPaths",
                "${rootDir}/storage/document/build/reports/jacoco/test/jacocoTestReport.xml, " +
                        "${rootDir}/storage/rdb/build/reports/jacoco/test/jacocoTestReport.xml"
        )
    }
}

subprojects {
    apply(plugin: "pmd")
    apply(plugin: "jacoco")
    apply(plugin: "checkstyle")
    apply(plugin: "java")
    apply(plugin: "java-library")
    apply(plugin: "org.springframework.boot")
    apply(plugin: "io.spring.dependency-management")
    apply(plugin: "application")
    apply(plugin: "com.google.cloud.tools.jib")

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // Test
        testImplementation("org.springframework.boot:spring-boot-starter-test")

        // QueryDsl
        implementation("com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta")

        // Lombok
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

        // AnnotationProcessor
        annotationProcessor("com.querydsl:querydsl-apt:${queryDslVersion}:jakarta")
        annotationProcessor("jakarta.annotation:jakarta.annotation-api")
        annotationProcessor("jakarta.persistence:jakarta.persistence-api")
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
        afterEvaluate {
            def exclusions = extractQClass()
            file("${rootDir}/config/jacoco/exclude-coverage.txt").eachLine {
                exclusions.add(it)
            }
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: exclusions)
            }))
        }
    }

    jacocoTestCoverageVerification {
        afterEvaluate {
            def exclusions = extractQClass()
            file("${rootDir}/config/jacoco/exclude-verification.txt").eachLine {
                exclusions.add(it)
            }
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: exclusions)
            }))

            violationRules {
                rule {
                    enabled = true
                    element = "CLASS"

                    limit {
                        counter = "LINE"
                        value = "TOTALCOUNT"
                        maximum = "200".toBigDecimal()
                    }
                    limit {
                        counter = "BRANCH"
                        value = "COVEREDRATIO"
                        minimum = 0.60
                    }
                }
            }
        }
    }

    tasks.test {
        jvmArgs '--add-opens', 'java.base/java.time=ALL-UNNAMED',
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    checkstyle {
        toolVersion = project.findProperty("checkStyleVersion")
        configFile = file("${rootDir}/config/lint/dailyge-rule.xml")
        configProperties = ["suppressionFile": file("${rootDir}/config/lint/dailyge-suppressions.xml")]
        maxWarnings = 0
    }

    pmd {
        toolVersion = project.findProperty("pmdVersion")
        pmdTest.enabled = true
        consoleOutput = true
        ignoreFailures = false
        ruleSetFiles = files("${rootDir}/config/lint/dailyge-pmd-rules.xml")
    }
}

static def extractQClass() {
    def qClasses = []
    for (qPattern in "QA".."QZ") {
        qClasses.add("**/*$qPattern*")
    }
    return qClasses
}
