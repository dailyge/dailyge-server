plugins {
    id("org.asciidoctor.jvm.convert")
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

apply(from: "${rootDir}/jib/dailyge-api/jib.gradle")
tasks.build.dependsOn(":storage:rdb:test")

dependencies {
    implementation(project(":storage:document"))
    implementation(project(":storage:rdb"))
    implementation(project(":storage:memory"))
    implementation(project(":support"))

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-client")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    implementation("io.jsonwebtoken:jjwt:0.9.1")
    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4")

    testImplementation("org.springframework.restdocs:spring-restdocs-restassured")
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:mysql")
    testImplementation("org.springframework.cloud:spring-cloud-contract-wiremock")

    asciidoctorExt("org.springframework.restdocs:spring-restdocs-asciidoctor")
}

springBoot {
    mainClassName = "project.dailyge.app.DailygeApplication"
}

ext {
    snippetsDir = file("build/generated-snippets")
}

test {
    dependsOn("copySubmoduleConfig")
    outputs.dir snippetsDir
    dependsOn(":storage:rdb:test")
}

asciidoctor {
    attributes "snippets": snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

tasks.register("copyDocument", Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into "static/docs"
    }
}

tasks.register("copySubmoduleConfig", Copy) {
    from("${rootDir}/config-module/dailyge-api")
    include("*.yml")
    into("${rootDir}/dailyge-api/src/main/resources")
}

sonarqube {
    properties {
        property("sonar.sources", "${rootDir}/dailyge-api/src/main/java")
        property("sonar.tests", "${rootDir}/dailyge-api/src/test")
        property(
                "sonar.coverage.jacoco.xmlReportPaths",
                "${rootDir}/dailyge-api/build/reports/jacoco/test/jacocoTestReport.xml"
        )
    }
}

tasks.named("processResources") {
    dependsOn("copySubmoduleConfig")
}
